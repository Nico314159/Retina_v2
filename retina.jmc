import "math";

new tags.blocks(fire) {
    "values": ["minecraft:fire", "minecraft:soul_fire"]
}
new tags.blocks(fluid) {
    "values": ["minecraft:water", "minecraft:lava"]
}
new tags.blocks(passable) {
    "values": [
        "minecraft:air",
        "minecraft:cave_air",
        "minecraft:void_air"
	]
}

class traverse {
    function setup() {

        $input_pitch = data get entity @s Rotation[1] 10;
        $input_yaw = data get entity @s Rotation[0] 10;
        Text.tellraw(@a, "&<!bold, white>Pitch: &<$input_pitch, red>, &<!bold, white>Yaw: &<$input_yaw, red>");
        math.gimbal_to_vec();
        Text.tellraw(@a, "Facing Vector: &<gold>[&<$output_vec3.x>, &<$output_vec3.y>, &<$output_vec3.z>]");

        $x = data get entity @s Pos[0];
        $y = data get entity @s Pos[1];
        $z = data get entity @s Pos[2];
        Text.tellraw(@a, "Origin: &<yellow>[&<$x>, &<$y>, &<$z>]");

        Hardcode.repeatList(
            "comp",
            () => {
                if ($output_vec3.comp < 0) $sign_comp = -1;
                if ($output_vec3.comp >= 0) $sign_comp = 1;

                $delta_comp = 1000000;
                $delta_comp /= $output_vec3.comp;
                $delta_comp /= $sign_comp;

                if ($output_vec3.comp == 0) $delta_comp = 2147483647;
                $distance_comp = $delta_comp;
            },
            ["x", "y", "z"]
        );

        $step = 0;
        $end = 0;
        traverse.loop();
    }
    function loop() {
        // Find nearest axis
        $shortest_distance = $distance_x;
        $shortest_distance < $distance_y;
        $shortest_distance < $distance_z;
        if ($shortest_distance = $distance_z) $nearest_axis = 2;
        if ($shortest_distance = $distance_y) $nearest_axis = 1;
        if ($shortest_distance = $distance_x) $nearest_axis = 0;
        
        // Process data based on nearest axis
        if ($nearest_axis = 0 && $step < 300 && $end = 0) traverse.x();
        if ($nearest_axis = 1 && $step < 300 && $end = 0) traverse.y();
        if ($nearest_axis = 2 && $step < 300 && $end = 0) traverse.z();

    }
    function x() {
        Text.tellraw(@a, "Distances to each block: &<green>[&<$distance_x>, &<$distance_y>, &<$distance_z>]");
        Text.tellraw(@a, "&<!bold, white>Closest direction: &<bold,aqua>X");
        $distance_x += $delta_x;
        if ($motion_end = 0) {
            $x += $sign_x;
            if ($sign_x = -1) execute positioned ~-1 ~0 ~0 run check.main();
            if ($sign_x = 1) execute positioned ~1 ~0 ~0 run check.main();
        }
    }
    function y() {
        Text.tellraw(@a, "Distances to each block: &<green>[&<$distance_x>, &<$distance_y>, &<$distance_z>]");
        Text.tellraw(@a, "&<!bold, white>Closest direction: &<bold,aqua>Y");
        $distance_y += $delta_y;
        if ($motion_end = 0) {
            $y += $sign_y;
            if ($sign_y = -1) execute positioned ~0 ~-1 ~0 run check.main();
            if ($sign_y = 1) execute positioned ~0 ~1 ~0 run check.main();
        }
    }
    function z() {
        Text.tellraw(@a, "Distances to each block: &<green>[&<$distance_x>, &<$distance_y>, &<$distance_z>]");
        Text.tellraw(@a, "&<!bold, white>Closest direction: &<bold,aqua>Z");
        $distance_z += $delta_z;
        if ($motion_end = 0) {
            $z += $sign_z;
            if ($sign_z = -1) execute positioned ~0 ~0 ~-1 run check.main();
            if ($sign_z = 1) execute positioned ~0 ~0 ~1 run check.main();
        }
    }
}

class check {
    function main() {
        $step++;
        $end = false;

        check.block.main();
        if ($end) check.collision.main();
        
        if ($step >= 300) $end = 1;
        if ($step >= 300) Text.tellraw(@a, "&<italic, light_purple>Failed to find anything within &<$step> blocks. End coordinates: &<bold>[&<$x>, &<$y>, &<$z>]");
        if ($end == 0) traverse.loop();
    }
    class block {
        function main() {
            if (!block ~ ~ ~ #retina:passable) $end = true;
            if ($end) {
                Text.tellraw(@a, "&<bold, underlined, gray>Block has been hit! Coordinates: &<blue>[&<$x>, &<$y>, &<$z>]");
                if ($debug_gold = 1) setblock ~ ~ ~ gold_block;
            }
            if (!$end) {
                Text.tellraw(@a, "&<italic, white>No block... Coordinates: &<blue>[&<$x>, &<$y>, &<$z>]");
            }
        }
    }
    class collision {
        function main() {
            if ($nearest_axis = 0) Text.tellraw(@a, "&<bold, white>Block hit direction: &<bold, dark_purple>X");
            if ($nearest_axis = 1) Text.tellraw(@a, "&<bold, white>Block hit direction: &<bold, dark_purple>Y");
            if ($nearest_axis = 2) Text.tellraw(@a, "&<bold, white>Block hit direction: &<bold, dark_purple>Z");
        }
    }
}